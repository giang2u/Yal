package yal.analyse ;

import java.util.*;
import yal.arbre.* ;
import yal.arbre.expression.* ;
import yal.exceptions.AnalyseSyntaxiqueException;
import yal.tds.*;

import java_cup.runtime.*;

action code {:
  //BlocDInstructions arbre = new BlocDInstructions(0) ;
 // BlocDInstructions arbreBoucle = new BlocDInstructions(0) ;

             :} ;

parser code {:

    public void report_error(String message, Object info) {

        HashMap<Integer, String> lesTerminaux = new HashMap<>() ;

        lesTerminaux.put(new Integer(CodesLexicaux.PLUS), "+") ;
        lesTerminaux.put(new Integer(CodesLexicaux.MOINS), "-") ;
        lesTerminaux.put(new Integer(CodesLexicaux.MULT), "*") ;
        lesTerminaux.put(new Integer(CodesLexicaux.DIV), "/") ;
        lesTerminaux.put(new Integer(CodesLexicaux.PAROUV), "(") ;
        lesTerminaux.put(new Integer(CodesLexicaux.PARFER), ")") ;
        lesTerminaux.put(new Integer(CodesLexicaux.SUP), ">") ;
        lesTerminaux.put(new Integer(CodesLexicaux.INF), "<") ;
        lesTerminaux.put(new Integer(CodesLexicaux.EGALEGAL), "==") ;
        lesTerminaux.put(new Integer(CodesLexicaux.DIFF), "!=") ;
        lesTerminaux.put(new Integer(CodesLexicaux.ET), "et") ;
        lesTerminaux.put(new Integer(CodesLexicaux.OU), "ou") ;
        lesTerminaux.put(new Integer(CodesLexicaux.NON), "non") ;

        StringBuffer m = new StringBuffer() ;

        if (info instanceof java_cup.runtime.Symbol) {
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);

            if (s.left >= 0) {
                m.append("\tligne : " + (s.left + 1)) ;
                if (s.right >= 0)
                    m.append(" colonne : " + (s.right+1)) ;
            }

            if (s.value != null) {
                lesTerminaux.put(CodesLexicaux.CONSTANTEINT, "" + s.value) ;
                lesTerminaux.put(CodesLexicaux.CONSTANTEBOOL, "" + s.value) ;
            }

            if (lesTerminaux.containsKey(new Integer(s.sym))) {
                m.append(" dernier token lu : " + lesTerminaux.get(new Integer(s.sym))) ;
            }
            else {
                m.append(" expression non termin√©e") ;
            }

        }
        throw new AnalyseSyntaxiqueException("" + m) ;
    }

    public void report_fatal_error(String message, Object info) {
        report_error(message, info);
    }
:};

/* ------------Declaration of Terminals and Non Terminals Section----------- */

/* Terminals (tokens returned by the scanner).  */

terminal Symbol    PLUS, MOINS, MULT, DIV, PAROUV, PARFER,EGAL, PROGRAMME,LONGUEUR ;
terminal Symbol    SUP, INF, EGALEGAL, DIFF, ET, OU, NON,POINTVIRGULE,POINT,CROCHOUV,CROCHFER;
terminal String    CONSTANTEINT, CONSTANTEBOOL,TYPE,IDF, FIN, DEBUT,ECRIRE,CSTECHAINE ;
terminal String    TANTQUE,FINTANTQUE,SI,SINON,FSI,ALORS,REPETER,LIRE,FONCTION,VIRGULE,RETOURNE ;


/* Non terminals used in the grammar section.  */

non terminal ArbreAbstrait PROG,LISTDECLARATION,LINST,DECLARATION,DECL_VAR, INSTRUCTION, AFFECT,RETOURNER,LISTEVARIABLESLOCALES;
non terminal ArbreAbstrait ECRITURE,LECTURE, BOUCLE,CONDITION,PARAMETRES,LISTEPARAM,DECL_FONCTION,PAR_EFF,DECL_TAB;
non terminal Expression EXPR, CSTE ;

precedence left OU ;
precedence left ET ;
precedence left EGALEGAL, DIFF;
precedence left INF, SUP ;
precedence left PLUS, MOINS;
precedence left MULT, DIV ;
precedence left NON ;

/* ----------------------------Grammar Section-------------------- */

start with PROG ;


PROG	::=		PROGRAMME IDF DEBUT LISTDECLARATION:d LINST:li FIN
				{: 
					BlocDInstructions bli=new BlocDInstructions(0);
					bli.ajouter(d);
					
					((BlocDInstructions)d).ajouter(li);
						
					RESULT=d; 
				:}
		        |
		        PROGRAMME IDF DEBUT LINST:i FIN
		        {: 
		        	RESULT=i; :}
		        ;

LISTDECLARATION   ::= LISTDECLARATION:lidecl  DECLARATION:decl
                {:
                  ((BlocDInstructions)lidecl).ajouter(decl);
                  RESULT=lidecl;
                :}
                |
                DECLARATION:d
                {:
					BlocDInstructions arbre = new BlocDInstructions(0) ;
            		arbre.ajouter(d);
                  	RESULT=arbre;
                :}
                ;

DECLARATION   ::= DECL_FONCTION:df
                {:
                	RESULT=df;
                :}
                |	
				DECL_VAR:dv
                {:
                	RESULT=dv;
                :}
                |
                DECL_TAB:dt
                {:
                	RESULT=dt;
                :}
                ;

DECL_VAR::=TYPE IDF:idf POINTVIRGULE
          {://rajoute les entres et symboles et tds
          	SymboleVariable s = new SymboleVariable(0);
          	s.setNumRegion(Tds.numRegionCourant);
          	EntreeVariable entre = new EntreeVariable(idf,Tds.numRegionCourant);
          	Tds.getInstance().ajouter(entre,s);
            RESULT=new Idf(idf,idfleft+1);
          :}
          |
          TYPE CROCHOUV EXPR:e CROCHFER IDF:idf POINTVIRGULE
          {:
          	//rajoute les entres et symboles et tds
          	SymboleTableau s = new SymboleTableau(0);
          	s.setNumRegion(Tds.numRegionCourant);
          	EntreeTableau entre = new EntreeTableau(idf,Tds.numRegionCourant);
          	Tds.getInstance().ajouter(entre,s);
            RESULT=new Tableau(idf,idfleft+1,e);
          :}
          ;

DECL_FONCTION::=FONCTION IDF:idf PARAMETRES:p DEBUT LINST:li FIN
		{:
			//Tds.getInstance().entreBlock();
			int nb=((BlocDInstructions)p).nbInstruction();
			
			SymboleFonction sf = new SymboleFonction(0);
			EntreeFonction ef = new EntreeFonction(idf,0,nb);
			Tds.getInstance().ajouter(ef,sf);
			BlocDInstructions bli=new BlocDInstructions(0);
			bli.ajouter(p);
			bli.ajouter(li);
			
			RESULT = new Fonction(idfleft++,idf,Tds.numRegionCourant,(BlocDInstructions)bli,nb);
						Tds.getInstance().sortieBlock();
			
		:}
		|
		FONCTION IDF:idf PARAMETRES:p DEBUT LISTEVARIABLESLOCALES:lv LINST:li FIN
		{:
			//Tds.getInstance().entreBlock();
			SymboleFonction sf = new SymboleFonction(0);
			EntreeFonction ef = new EntreeFonction(idf,0,
			((BlocDInstructions)p).nbInstruction());
			Tds.getInstance().ajouter(ef,sf);
			BlocDInstructions bli=new BlocDInstructions(0);
			bli.ajouter(p);
			bli.ajouter(lv);
			bli.ajouter(li);
			int nb=((BlocDInstructions)p).nbInstruction();
			RESULT = new Fonction(idfleft++,idf,Tds.numRegionCourant,(BlocDInstructions)bli,nb);
						Tds.getInstance().sortieBlock();
			
		:}
		;
		
PARAMETRES ::= PAROUV PARFER{:
			Tds.getInstance().entreBlock();	
			RESULT = new BlocDInstructions(0);	
		:}
		|
		PAROUV LISTEPARAM:listp PARFER
		{:
			Tds.getInstance().entreBlock();
			for(ArbreAbstrait aa : ((BlocDInstructions)listp).getexpr()) {
				SymboleVariable s = new SymboleVariable(0);
	          	s.setNumRegion(Tds.numRegionCourant);
				
          		EntreeVariable entre = new EntreeVariable(((Idf)aa).getNom(),Tds.numRegionCourant);
          		((Idf)aa).setNumRegion(Tds.numRegionCourant);
          		Tds.getInstance().ajouter(entre,s);
          	
			}		
			RESULT = listp;
			
		:};
LISTEPARAM ::=TYPE IDF:i 
		{:
			BlocDInstructions arbre = new BlocDInstructions(0) ;
    		arbre.ajouter(new Idf(i,ileft+1));
          	RESULT=arbre;   
          	     
		:}
		| TYPE IDF:i POINTVIRGULE LISTEPARAM:listp
		{:

		    ((BlocDInstructions)listp).ajouter(new Idf(i,ileft+1));
	       	RESULT=listp;
		:}
		;
LISTEVARIABLESLOCALES ::=DECL_VAR:dv 
		{:
		
			BlocDInstructions arbre = new BlocDInstructions(0) ;
    		arbre.ajouter(dv);
          	RESULT=arbre;   
          	     
		:}
		| DECL_VAR:dv LISTEVARIABLESLOCALES:lidecl
		{:
	       ((BlocDInstructions)lidecl).ajouter(dv);
	       RESULT=lidecl;
		:}
		;

LINST	::= LINST:li INSTRUCTION:i

		{: ((BlocDInstructions)li).ajouter(i);
			RESULT=li ;
			 
		:}
		| INSTRUCTION:i
		{: 
			BlocDInstructions arbre = new BlocDInstructions(0) ;		
			arbre.ajouter(i);
			RESULT=arbre; 
		:}
		;

INSTRUCTION   ::=   AFFECT:a
		{:
            RESULT=a;
        :}
        | ECRITURE:e
        {:
          RESULT=e;
        :}
        | BOUCLE:b
        {:
          RESULT=b;
        :}
        | CONDITION:c
        {:
          RESULT=c;
        :}
        | LECTURE:le
        {:
        RESULT=le;
        :}
        |
        RETOURNER:re
        {:
        	RESULT=re;
        :}
        ;

AFFECT   ::=   IDF:i   EGAL   EXPR:e   POINTVIRGULE
		{:
        	RESULT = new Affectation(eleft+1, new Idf(i,ileft+1), e);
        :}
         |
         IDF:idf CROCHOUV EXPR:i CROCHFER EGAL   EXPR:e   POINTVIRGULE
		{:
        	RESULT = new AffectationTableau(eleft+1, idf,i, e);
        :}
		;
ECRITURE::= ECRIRE EXPR:e POINTVIRGULE
        {:
          RESULT=new Ecrire(e,eleft+1);
        :}
        |
        ECRIRE CSTECHAINE:csteCH POINTVIRGULE
        {:
          StockChaine.getInstance().ajouter(csteCH);
          RESULT=new Ecrire(csteCH,csteCHleft+1);
        :};
LECTURE::=LIRE IDF:e POINTVIRGULE
        {:
        Idf id = new Idf(e,eleft, Tds.numRegionCourant);
          RESULT=new Lire(id,eleft+1);
        :};

        
CONDITION::=SI EXPR:e ALORS FSI
      {:
		RESULT=new Condition(e,eleft+1);		
      :}
      | SI EXPR:e ALORS LINST:li FSI
      {:
		RESULT=new Condition(e,(BlocDInstructions)li,eleft+1);
      :}
      |  SI EXPR:e ALORS SINON LINST:lisinon FSI
      {:
      		
      		RESULT=new Condition(e,eleft+1,(BlocDInstructions)lisinon);
      
      :}
      | 
      SI EXPR:e ALORS LINST:lisi SINON LINST:lisinon FSI
      {:
 
      		RESULT=new Condition(e,(BlocDInstructions)lisi,eleft+1,(BlocDInstructions)lisinon);
      
      :}
      ;
      
BOUCLE::=TANTQUE EXPR:e REPETER  LINST:lis FINTANTQUE
      {:	
      		BlocDInstructions bloc = new BlocDInstructions(0);
			bloc.ajouter(lis);
			RESULT= new BoucleTANT(eleft+1,e,bloc);
      :};
RETOURNER::=RETOURNE EXPR:e POINTVIRGULE
		{:
		RESULT=new RetourneExpression(eleft+1,e,Tds.numRegionCourant);
		:};

EXPR    ::=     EXPR:g  PLUS  EXPR:d
                {: RESULT = new Plus(g, d) ; :}
        |
                EXPR:g  MOINS  EXPR:d
                {: RESULT = new Moins(g, d); :}
        |
                EXPR:g  MULT  EXPR:d
                {: RESULT = new Mult(g, d); :}
        |
                EXPR:g  DIV  EXPR:d
                {: RESULT = new Div(g, d); :}
        |
                EXPR:g  INF  EXPR:d
                {: RESULT = new Inferieur(g, d); :}
        |
                EXPR:g  SUP  EXPR:d
                {: RESULT = new Superieur(g, d); :}
        |
                EXPR:g  EGALEGAL  EXPR:d
                {: RESULT = new Egal(g, d); :}
        |
                EXPR:g  DIFF  EXPR:d
                {: RESULT = new Different(g, d); :}
        |
                MOINS  EXPR:e
                {: RESULT = new MoinsUnaire(e); :}
        |
                EXPR:g  ET  EXPR:d
                {: RESULT = new EtLogique(g, d); :}
        |
                EXPR:g  OU  EXPR:d
                {: RESULT = new OuLogique(g, d); :}
        |
                NON EXPR:e
                {: RESULT = new NonLogique(e); :}
        |
                CSTE:c
                {: RESULT = c ; :}
        |
                PAROUV  EXPR:e  PARFER
                {: RESULT = e ; :}
        |       IDF:e
                {:
                  RESULT=new Idf(e,eleft+1);
                :}
        |		IDF:e PAROUV PAR_EFF:peff PARFER
        		{:
        			int nb=((BlocDInstructions)peff).nbInstruction();
        			RESULT=new AppelFonction(e,eleft+1,nb,((BlocDInstructions)peff));
        		:}
        |		IDF:e PAROUV PARFER
        		{:
        			RESULT=new AppelFonction(e,eleft+1);
        		:}
		|		IDF:e POINT LONGUEUR 
				{:
					RESULT=new TableauLongueur(e,eleft+1);
				:}
        |		IDF:e CROCHOUV EXPR:indice CROCHFER 
		{:
				RESULT=new ElementTableau(e,eleft+1,indice);
		:}
                
                
                ;
                
PAR_EFF::= EXPR:e
		{:
			BlocDInstructions blo=new BlocDInstructions(0);		
			blo.ajouter(e);
			RESULT=blo;
		:}
		| EXPR:e VIRGULE PAR_EFF:peff
		{:
			((BlocDInstructions)peff).ajouter(e);
			RESULT=peff ;
		:};
CSTE    ::=     CONSTANTEINT:e
                {: RESULT = new ConstanteEntiere(e, eleft+1) ; :}
        |       CONSTANTEBOOL:e
                {: RESULT = new ConstanteBool(e, eleft+1) ; :}
;